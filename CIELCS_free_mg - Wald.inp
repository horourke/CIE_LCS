!***********************************************!
!         O'Rourke & Hilley - CIE LCS           !
!***********************************************!

TITLE: O'Rourke & Hilley CIE LCS - MG, free b, Wald test;
!Approach = MG, params = free;
DATA:	FILE IS CIELCS_free.dat;
  VARIABLE:	NAMES ARE z x1 m2-m6 y2-y6;
    	GROUPING IS z (0=mod0 1=mod1);
     USEVARIABLE ARE x1 m2-m6 y2-y6;
     MISSING are all (999);
  ANALYSIS:
     ITERATIONS = 10000;
     COVERAGE = 0;
     !BOOTSTRAP = 5000;
  MODEL:
  !for the longitudinal variable Y

  !Latent variable nodes
    ly2 by y2 @1;
    ly3 by y3 @1;
    ly4 by y4 @1;
    ly5 by y5 @1;
    ly6 by y6 @1;

  !Autoregressive part
    ly3 on ly2 @1;
    ly4 on ly3 @1;
    ly5 on ly4 @1;
    ly6 on ly5 @1;

  !Difference score on latent level (LCS scores)
    dy3 by ly3 @1;
    dy4 by ly4 @1;
    dy5 by ly5 @1;
    dy6 by ly6 @1;

  !Proportional change
    dy3 on ly2 * (pcy);
    dy4 on ly3 * (pcy);
    dy5 on ly4 * (pcy);
    dy6 on ly5 * (pcy);

  !Intercept
    y0 by ly2 @1;

  !Slope: Model relationship between slope and LCSs
    sy by dy3 @1;
    sy by dy4 @1;
    sy by dy5 @1;
    sy by dy6 @1;

  ! Set the means and variance of observed, latent, and LCSs to be 0
    [y2@0]; [ly2@0];  ly2@0;
    [y3@0]; [ly3@0]; [dy3@0]; ly3@0; dy3@0;
    [y4@0]; [ly4@0]; [dy4@0]; ly4@0; dy4@0;
    [y5@0]; [ly5@0]; [dy5@0]; ly5@0; dy5@0;
    [y6@0]; [ly6@0]; [dy6@0]; ly6@0; dy6@0;

  !for the longitudinal variable M

  !Latent variable nodes
    lm2 by m2 @1;
    lm3 by m3 @1;
    lm4 by m4 @1;
    lm5 by m5 @1;
    lm6 by m6 @1;

  !Autoregressive part
    lm3 on lm2 @1;
    lm4 on lm3 @1;
    lm5 on lm4 @1;
    lm6 on lm5 @1;

  !Difference score on latent level (LCS scores)
    dm3 by lm3 @1;
    dm4 by lm4 @1;
    dm5 by lm5 @1;
    dm6 by lm6 @1;

  !Proportional change
    dm3 on lm2 * (pcm);
    dm4 on lm3 * (pcm);
    dm5 on lm4 * (pcm);
    dm6 on lm5 * (pcm);

  !Intercept
    m0 by lm2 @1;

  !Slope: Model relationship between slope and LCSs
    sm by dm3 @1;
    sm by dm4 @1;
    sm by dm5 @1;
    sm by dm6 @1;

  ! Set the means and variance of observed, latent, and LCSs to be 0
    [m2@0]; [lm2@0];  lm2@0;
    [m3@0]; [lm3@0]; [dm3@0]; lm3@0; dm3@0;
    [m4@0]; [lm4@0]; [dm4@0]; lm4@0; dm4@0;
    [m5@0]; [lm5@0]; [dm5@0]; lm5@0; dm5@0;
    [m6@0]; [lm6@0]; [dm6@0]; lm6@0; dm6@0;

  !Intercept & slope means & variances
    [y0*] (y0);
    [sy*] (sy);
    [m0*] (m0);
    [sm*] (sm);
    y0* (y0v);
    sy* (syv);
    m0* (m0v);
    sm* (smv);

    !X on slope Y (c');
    sy on x1 (cp);

    !X on slope of M (a);
    sm on x1 (a);

  !coupling from m to y (b);
  dy3 on lm2*-.23 (b3);
  dy4 on lm3*-.18 (b4);
  dy5 on lm4*-.13 (b5);
  dy6 on lm5*-.08 (b6);

    !Set all residuals to be equal
    y2*1 (5); m2*1 (6);
    y3*1 (5); m3*1 (6);
    y4*1 (5); m4*1 (6);
    y5*1 (5); m5*1 (6);
    y6*1 (5); m6*1 (6);

    ! Set the correlated residuals
    y2 with m2* (7);
    y3 with m3* (7);
    y4 with m4* (7);
    y5 with m5* (7);
    y6 with m6* (7);

    !X on M & Y intercept;
    m0 ON x1 (i);
    y0 on x1;

    !Parameter covs;
    y0 WITH sy;
    m0 WITH sm;
    m0 WITH y0;
    sm WITH sy;
    m0 WITH sy;
    y0 WITH sm;

MODEL mod0:
  !Intercept & slope means & variances
    [y0*] (y0);
    [sy*] (sy);
    [m0*] (m0);
    [sm*] (sm);
    y0* (y0v);
    sy* (syv);
    m0* (m0v);
    sm* (smv);

   !X on slope M (a);
    sm on x1 (a0);

    !X & Z on M & Y intercept;
    m0 ON x1 (i0);


MODEL mod1:
  !Intercept & slope means & variances
    [y0*] (y0);
    [sy*] (sy);
    [m0*] (m0);
    [sm*] (sm);
    y0* (y0v);
    sy* (syv);
    m0* (m0v);
    sm* (smv);

    !X on slope M (a);
    sm on x1 (a1);

    !X & Z on M & Y intercept;
    m0 ON x1 (i1);

    !Indirect effect ab;
  Model Constraint:
  New(ab0_3 ab0_4 ab0_5 ab0_6 ab1_3 ab1_4 ab1_5 ab1_6);
  ab0_3 = a0*b3;
  ab0_4 = a0*b4;
  ab0_5 = a0*b5;
  ab0_6 = a0*b6;
  ab1_3 = a1*b3;
  ab1_4 = a1*b4;
  ab1_5 = a1*b5;
  ab1_6 = a1*b6;

MODEL TEST:
  ab0_3 = ab1_3;
  ab0_4 = ab1_4;
  ab0_5 = ab1_5;
  ab0_6 = ab1_6;

  OUTPUT:  TECH1 TECH4;
  !cint(bootstrap);